#compdef devhost

_devhost() {
  local -a commands
  commands=(
    "add:Add a mapping"
    "remove:Remove a mapping"
    "list:List mappings"
    "url:Print URL (Ctrl+O to open)"
    "open:Open URL"
    "validate:Quick health checks"
    "export:Print generated Caddyfile"
    "edit:Open devhost.json in editor"
    "resolve:Show DNS and port reachability"
    "doctor:Deeper diagnostics"
    "diagnostics:Export diagnostic bundle"
    "info:Show help"
    "domain:Show or set base domain"
    "hosts:Manage hosts entries"
    "caddy:Manage Caddy (Windows)"
    "start:Start router"
    "stop:Stop router"
    "status:Router status"
    "install:Run installer"
  )

  _describe -t commands 'devhost commands' commands

  # dynamic names from devhost.json (best-effort)
  local -a names
  if command -v jq >/dev/null 2>&1; then
    names=(${(f)"$(jq -r 'keys[]' devhost.json 2>/dev/null)"})
  elif command -v python3 >/dev/null 2>&1; then
    names=(${(f)"$(python3 - <<'PY' 2>/dev/null
import json
try:
    data=json.load(open("devhost.json"))
    print("\n".join(sorted(data.keys())))
except Exception:
    pass
PY
)"})
  fi

  case $words[2] in
    remove|url|open|resolve)
      if (( ${#names[@]} )); then
        _arguments '1:name:->names' && return 0
        _values 'names' $names
      fi
      ;;
    add)
      _arguments '1:option:(--http --https)'
      ;;
    list)
      _arguments '*:option:(--json)'
      ;;
    export)
      _arguments '1:subcommand:(caddy)'
      ;;
    status)
      _arguments '*:option:(--json)'
      ;;
    install)
      _arguments '*:option:(--macos --windows --linux --caddy --yes --dry-run --start-dns --install-completions --domain --uvicorn --user --clean)'
      ;;
    doctor)
      _arguments '*:option:(--windows)'
      ;;
    diagnostics)
      case $words[3] in
        preview)
          _arguments '*:option:(--no-state --no-config --no-proxy --no-logs --top --max-size --no-size-limit --redaction-file --redact --no-redact)'
          ;;
        export)
          _arguments '*:option:(--no-state --no-config --no-proxy --no-logs --output -o --max-size --no-size-limit --redaction-file --redact --no-redact)'
          ;;
        upload)
          _arguments '*:option:(--max-size --no-size-limit --redaction-file --redact --no-redact)'
          ;;
        *)
          _arguments '1:subcommand:(preview export upload)'
          ;;
      esac
      ;;
    hosts)
      _arguments '1:subcommand:(sync clear)'
      ;;
    caddy)
      _arguments '1:subcommand:(start stop restart status)'
      ;;
  esac
}

_devhost "$@"
